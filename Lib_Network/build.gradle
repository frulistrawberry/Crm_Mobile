apply plugin: 'com.android.library'
apply plugin:'maven-publish'
android {

    namespace 'com.baihe.lib_network'

    compileSdk rootProject.android.compileSdk

    defaultConfig {
        minSdkVersion rootProject.android.minSdk
        targetSdkVersion rootProject.android.targetSdk
        versionCode 1
        versionName "1.0"
    }

    // 使用 JDK 1.8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'META-INF/*******'
    }
}


dependencies {
    implementation rootProject.depsLibs.okhttp
    implementation rootProject.depsLibs.okio
    implementation rootProject.depsLibs.lifecycleOnly
}




task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.getSourceFiles()
    classifier 'sources'
}

afterEvaluate{
    publishing {
        publications {
            mavenAar(MavenPublication) {
                groupId 'com.wedding.crm.lib'
                artifactId 'network'
                version '1.0.0.0-SNAPSHOT'
                afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
                artifact generateSourcesJar
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
                            println "dependency=${it.toString()}"
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'implementation')

                        }
                    }
                }
            }
        }
        repositories {
            maven {
                allowInsecureProtocol = true
                url 'http://10.240.210.158:8081/repository/jiayuan/'
                credentials {
                    username = 'jiayuandev'
                    password = 'jyandroid'
                }
            }
            mavenLocal()
        }
    }

}
